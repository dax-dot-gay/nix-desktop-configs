{
    config,
    lib,
    hostname,
    ...
}:
with lib;
let
    cfg = config.flake.secrets;
    sops = config.sops;
    secret-options = types.submodule (
        { config, ... }:
        {
            name = mkOption {
                type = types.str;
                default = config._module.args.name;
                description = ''
                    Name of the file used in /run/secrets
                '';
            };
            neededForUsers = mkOption {
                type = types.bool;
                default = false;
                description = ''
                    Enabling this option causes the secret to be decrypted before users and groups are created.
                    This can be used to retrieve user's passwords from sops-nix.
                    Setting this option moves the secret to /run/secrets-for-users and disallows setting owner and group to anything else than root.
                '';
            };
            owner = mkOption {
                type = types.str;
                default = "root";
                description = ''
                    Sets the owner of this secret (defaults to root)
                '';
            };
            group = mkOption {
                type = types.nullOr types.str;
                default = if config.owner != null then users.${config.owner}.group else null;
                defaultText = literalMD "{option}`config.users.users.\${owner}.group`";
                description = ''
                    Group of the file. Can only be set if gid is 0.
                '';
            };
            key = mkOption {
                type = types.str;
                default = if sops.defaultSopsKey != null then sops.defaultSopsKey else config._module.args.name;
                description = ''
                    Key used to lookup in the sops file.
                    No tested data structures are supported right now.
                    This option is ignored if format is binary.
                    "" means whole file.
                '';
            };
            path = mkOption {
                type = types.str;
                default =
                    if config.neededForUsers then
                        "/run/secrets-for-users/${config.name}"
                    else
                        "/run/secrets/${config.name}";
                defaultText = "/run/secrets-for-users/$name when neededForUsers is set, /run/secrets/$name when otherwise.";
                description = ''
                    Path where secrets are symlinked to.
                    If the default is kept no symlink is created.
                '';
            };
            format = mkOption {
                type = types.enum [
                    "yaml"
                    "json"
                    "binary"
                    "dotenv"
                    "ini"
                ];
                default = sops.defaultSopsFormat;
                description = ''
                    File format used to decrypt the sops secret.
                    Binary files are written to the target file as is.
                '';
            };
            mode = mkOption {
                type = types.str;
                default = "0400";
                description = ''
                    Permissions mode of the in octal.
                '';
            };
            restartUnits = mkOption {
                type = types.listOf types.str;
                default = [ ];
                example = [ "sshd.service" ];
                description = ''
                    Names of units that should be restarted when this secret changes.
                    This works the same way as <xref linkend="opt-systemd.services._name_.restartTriggers" />.
                '';
            };
            reloadUnits = mkOption {
                type = types.listOf types.str;
                default = [ ];
                example = [ "sshd.service" ];
                description = ''
                    Names of units that should be reloaded when this secret changes.
                    This works the same way as <xref linkend="opt-systemd.services._name_.reloadTriggers" />.
                '';
            };
        }
    );
in
{
    options.flake.secrets = {
        global = mkOption {
            type = types.attrsOf secret-options;
            default = { };
            description = "Secrets to load from global secret file";
        };
        local = mkOption {
            type = types.attrsOf secret-options;
            default = { };
            description = "Secrets to load from local secret file";
        };
    };
    config = {
        sops = {
            defaultSopsFile = ../../secrets/secrets.yaml;
            age.sshKeyPaths = [ "/etc/ssh/ssh_host_ed25519_key" ];
            secrets =
                (lib.mapAttrs (name: value: value // { sopsFile = ../../secrets/secrets.yaml; }) cfg.global)
                // (lib.mapAttrs (
                    name: value: value // { sopsFile = ../../secrets/machines/${hostname}/secrets.yaml; }
                ) cfg.local);
        };
    };
}
